target Python {
    files: [
        "ROS/carla_intersection/src/rsu.py",
        "ROS/carla_intersection/src/utils.py"
    ]
};

preamble {=
    from rsu import RSU
    from utils import GenericClock, dotdict, make_coordinate
=}

reactor RSU(
    num_entries(0),
    intersection_width(0),
    nominal_speed_in_intersection(0.0),
    intersection_position(0, 0, 0)
) {
    input[num_entries] request_;
    output[num_entries] grant_;

    preamble {=
        class Logger:
            def info(self, *args):
                print(f"{get_logical_time()} - rsu: ", args)

        class LFClock(GenericClock):
            def get_current_time_in_ns(self):
                return get_logical_time()
    =}

    reaction(startup) {=
        self.rsu = RSU(intersection_width = self.intersection_width,
                       nominal_speed_in_intersection = self.nominal_speed_in_intersection,
                       intersection_position = make_coordinate(self.intersection_position),
                       clock = self.LFClock(),
                       logger = self.Logger())
    =}

    reaction(request_) -> grant_ {=
        for i in range(self.num_entries):
            if not request_[i].is_present:
                continue
            request = request_[i].value
            pub_packets = self.rsu.receive_request(request)
            if pub_packets.grant != None:
                grant = dotdict()
                grant.intersection_position = pub_packets.grant.intersection_position
                grant.target_speed = pub_packets.grant.target_speed
                grant.arrival_time = pub_packets.grant.arrival_time
                grant_[i].set(grant)
    =}
}